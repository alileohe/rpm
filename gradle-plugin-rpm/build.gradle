plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.0'
}

group = 'org.xbib.gradle.plugin'

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.gradle.plugin-publish'

dependencies {
    compile gradleApi()
    compile project(':rpm-core')
    compileOnly "org.codehaus.groovy:groovy:${project.property('groovy.version')}"
    testCompile "junit:junit:${project.property('junit.version')}"
    testCompile("org.spockframework:spock-core:${project.property('spock-core.version')}") {
        exclude module: 'groovy'
        exclude module: 'junit'
    }
    testCompile "org.xbib:guice:${project.property('xbib-guice.version')}"

}

compileGroovy {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

if (project.hasProperty('gradle.publish.key')) {
    pluginBundle {
        website = 'https://github.com/xbib/rpm'
        vcsUrl = 'https://github.com/xbib/rpm'
        plugins {
            rpmPlugin {
                id = 'org.xbib.gradle.plugin.rpm'
                version = project.version
                description = projectDescription
                displayName = projectDescription
                tags = ['gradle', 'plugin', 'rpm']
            }
        }
    }
}
